buildscript {
    ext {
        springBootVersion = '3.3.0'
        springDependencyManagementVersion = '1.1.5'
        jooqGradlePlugin = '9.0'
        jooqVersion = '3.19.9'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
    id 'nu.studer.jooq' version "${jooqGradlePlugin}"
}

group = 'yamsroun'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-jooq') {
        exclude group: 'org.jooq:jooq'
    }
    implementation "org.jooq:jooq:${jooqVersion}"
    runtimeOnly 'com.mysql:mysql-connector-j'

    jooqGenerator project(':entity')
    jooqGenerator project(':jOOQ-custom')

    jooqGenerator "org.jooq:jooq:${jooqVersion}"
    jooqGenerator "org.jooq:jooq-meta:${jooqVersion}"
    jooqGenerator "org.jooq:jooq-meta-extensions-hibernate:${jooqVersion}"
    jooqGenerator 'com.h2database:h2:1.4.200'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

String dbUser = System.properties["db-user"] ?: "root"
String dbPasswd = System.properties["db-passwd"] ?: "passwd"

jooq {
    version = "${jooqVersion}"
    configurations {
        sakilaDb {
            generationTool {
                // DB 접속정보
                //jdbc {
                //    driver = 'com.mysql.cj.jdbc.Driver'
                //    url = 'jdbc:mysql://localhost:33061'
                //    user = "${dbUser}"
                //    password = "${dbPasswd}"
                //}


                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'

                    database {
                        //name = 'org.jooq.meta.mysql.MySQLDatabase'
                        name = 'org.jooq.meta.extensions.jpa.JPADatabase'
                        unsignedTypes = true //Default

                        //schemata {
                        //    schema {
                        //        inputSchema = 'sakila'
                        //    }
                        //}

                        properties {
                            property {
                                key = 'packages'
                                value = 'yamsroun.entity'
                            }

                            property {
                                key = 'useAttributeConverters' //2순위
                                value = true
                            }
                        }

                        forcedTypes { //1순위
                            forcedType {
                                userType = 'java.lang.Long'
                                includeTypes = 'int unsigned'
                            }

                            forcedType {
                                userType = 'java.lang.Integer'
                                includeTypes = 'tinyint unsigned'
                            }

                            forcedType {
                                userType = 'java.lang.Integer'
                                includeTypes = 'smallint unsigned'
                            }
                        }
                    }

                    generate {
                        daos = true
                        records = true
                        //immutablePojos = true
                        fluentSetters = true
                        javaTimeTypes = true
                        deprecated = false

                        //jpaAnnotations = true
                        //jpaVersion = 2.2
                        //validationAnnotations = true
                        //springAnnotations = true
                        //springDao = true
                    }

                    target {
                        directory = 'src/generated'
                    }

                    //TODO 왜 서브모듈로 별도 분리해야 하는 걸까?
                    strategy.name = 'jooq.custom.generator.JPrefixGeneratorStrategy'
                }
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", "src/generated"]
        }
    }
}
